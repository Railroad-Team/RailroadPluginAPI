plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'io.github.railroad'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(21)
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

javafx {
    version = '21.0.2'
    modules = [ 'javafx.controls', 'javafx.graphics' ]
}

dependencies {
    api 'io.github.railroad:RailroadLogger:1.0-SNAPSHOT'
    api 'io.github.railroad:RailroadCore:1.0-SNAPSHOT'

    api 'org.openjfx:javafx-controls:21.0.2'
    api 'org.openjfx:javafx-graphics:21.0.2'
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    repositories {
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact tasks.sourcesJar
        }
    }
}